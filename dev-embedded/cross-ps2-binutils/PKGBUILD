# Contributor: ragnarok2040 at gmail dot com

pkgname=cross-ps2-binutils
pkgver=2.14
pkgrel=1
pkgdesc="Binutils for the Playstation2"
arch=('i686' 'x86_64')
depends=()
license=('GPL')
url="http://www.ps2dev.org/"
makedepends=('make' 'gcc34' 'patch')
options=('!strip')

source=(http://ftp.gnu.org/gnu/binutils/binutils-2.14.tar.bz2
        binutils-2.14-PS2.patch
        binutils-destdir.patch)

md5sums=('2da8def15d28af3ec6af0982709ae90a'
         'b331667eefe53f600acd99d46ac5475b'
         'ef80d0e44de3c2c9509d890cb86fa601')

_prefix=opt/ps2dev

build() {
  # Set environment variables
  unset CFLAGS CXXFLAGS

  # Patch the sources
  msg "Patching binutils..."
  cd $srcdir
  patch -p0 < binutils-2.14-PS2.patch || return 1
  patch -p0 < binutils-destdir.patch || return 1

  # Build binutils per target
  for _target in "ee" "iop" "dvp"; do

    cd $srcdir

    msg "Building binutils for $TARGET..."

    mkdir "$_target-binutils" && cd "$_target-binutils" || return 1

    CC=gcc-3.4 ../binutils-2.14/configure --prefix="/$_prefix/$_target" --target="$_target" || return 1

    CC=gcc-3.4 make clean
    CC=gcc-3.4 make || return 1
    CC=gcc-3.4 make -j1 DESTDIR=$pkgdir install || return 1

    msg "Removing build directory..."
    rm -rf $srcdir/$_target-binutils || return 1
   
    msg "Stripping symbols from binaries..."
    cd $pkgdir/$_prefix/$_target/bin
    find . | xargs file | grep "executable" | grep ELF | cut -f 1 -d : | xargs strip --strip-unneeded 2> /dev/null

    cd $pkgdir/$_prefix/$_target/$_target/bin
    find . | xargs file | grep "executable" | grep ELF | cut -f 1 -d : | xargs strip --strip-unneeded 2> /dev/null

  done
}